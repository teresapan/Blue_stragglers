## -*-SM-*- Set SM-mode in emacs
Box3		## Top right box
		SET x = -20,20,2.5
		SET y = histogram(eps:x)
		LIMITS x y
		BOX
		HISTOGRAM x y
		stats eps mean sig kurt
		SET xx=-20,20,.5
		SET gg=gauss(xx)
		SET gg=gg*(x[1]-x[0])*dimen(eps)
		CONNECT xx gg
		XLABEL \delta (km/s)
		ANGLE 360 YLABEL N\n ANGLE 0
Box2		## bottom right box
		SET x=0,3*PI,PI/30
		SET c=COS(x) SET s=SIN(x)
		LIMITS x c
		BOX
		CONNECT x c
		CONNECT x s
		SET s=reverse(s) SET rx=reverse(x)
		ANGLE 90
		SHADE 500 (x CONCAT rx) (c CONCAT s)
		ANGLE 360 YLABEL \Theta\n ANGLE 0
		XLABEL x
Box1		## Bottom left box
		LIMITS .7 1.5 17.5 14.5
		BOX
		IF($?default_font == 1) {
		   DEFINE 1 $default_font
		} ELSE {
		   define 1 rm
		}
		DEFINE ptype |
		DEFINE default_font "ti"
		EXPAND .5 PTYPE name POINTS BV V EXPAND 1.001
		PTYPE $ptype
		DEFINE ptype DELETE
		DEFINE default_font $1
		XLABEL B-V
		ANGLE 360 YLABEL V\n ANGLE 0
old_Box4	## Top left box
		square 20
		XLABEL \xi
		ANGLE 360 YLABEL \psi\n ANGLE 0
Box4    	## Top left box
		draw_fresnel
		set xx=-1,1,.1 
		VIEWPOINT 30 20 10
		Surface 3  -.6 .9  xx xx
		box3
		label3 x \xi
		label3 y \eta
		label3 z J_0
cover		# draw the cover
		IF($?start == 0) {
		   DEFINE start 1
		   set_cover
		   load demos
		   load surfaces
		   load math
		}
		DEFINE expand | expand 1.001
		DEFINE 1 $?TeX_strings DEFINE TeX_strings 1
		WINDOW 2 3 1 1
		Box1
		WINDOW 2 3 2 1
		Box2
		WINDOW 2 3 2 3
		Box3
		WINDOW 2 3 1 3
		Box4
		WINDOW 1 1 1 1
		LIMITS 0 1 -0.05 .85
		RELOCATE .5 .5
		EXPAND 4
		PUTLABEL 5 \2\oe SM
		EXPAND 1.001
		RELOCATE .5 .43
		local define p ( index('$version', '(') )
		if($p == 13) {
		   local define version "(unknown version)"
		} else {
		   local define v "$!version"
		   local define version ( substr('$v', 14, $p-15) )
		   # Replace _ by .  SM makes this hard...
		   while {1} {
		      define p ( index('$version', '_') )
		      if($p < 0) { break }
		      define version ( substr('$version',0,$p) + '.' + \
		          substr('$version', $p + 1, strlen('$version') - $p) )
		   }
		}
		PUTLABEL 5 \2\rm{}$(quote_TeX('$version'))
		RELOCATE .5 .39
		PUTLABEL 5 Robert Lupton
		RELOCATE .5 .365
		PUTLABEL 5 \firtree{} Princeton University \firtree
		RELOCATE .5 .33
		PUTLABEL 5 Patricia Monger
		RELOCATE .5 .305
		PUTLABEL 5 \snow{} McMaster University \snow
		EXPAND $expand DEFINE expand DELETE
		IF($?1 == 0) { DEFINE TeX_Strings DELETE }
set_cover	## Setup vectors for cover
		SET hole={58 -59 60 61 -62 -63 64 -66 67 -68 69 70 71 -72 73 74 76 77 \
		79 80 82 83 84 86 87 88 90 -91 93 95 97 -98 100 101 102 104 107}
		SET V={15.73 16.38 16.61 16.38 16.66 16.62 16.37 15.67 16.36 16.48 \
		16.21 16.32 16.65 16.57 16.53 16.51 15.70 16.89 16.6 15.72 \
		16.56 16.27 16.32 15.55 15.82 16.69 15.92 16.99 16.36 16.06 \
		15.80 15.01 15.76 16.09 16.50 16.46 16.13 }
		SET BV={0.88 1.07 1.09 1.24 1.3 1.1 1.25 1.2 1.14 1.0 1.28 1.10 1.17 \
		1.01 1.28 1.20 0.82 1.03 1.03 1.37 1.09 0.99 1.18 1.14 0.82 \
		1.00 1.16 1.2 1.22 0.83 1.33 1.28 0.84 0.97 1.00 1.19 1.07 }
		SET name={C-I1 C-I2 C-I3 CI-4 CII-1 CII-2 CII-4 \
		    CIII-1 CIII-4 CIV-1 CIV-2 CIV-9 \
		    B-I1 B-I2 BI-4 BII-1 BII-2 BII-3 BII-4 \
		    BII-5 BIII-1 BIV-5 \
		    A-I1 A-I3 AI-4 AII-1 AII-2  \
		    AIII-2 AIV-5 }
		SET eps={-2.1600 -4.0320 0.1440 4.1760 -2.0160 0.0000 -2.7360 \
		           138.3840 0.0000 3.4560 3.1680 2.0160 -3.1680 1.2960 7.0560 \
		        3.0240 0.0000 -0.1440 2.4480 3.3120 -4.3200 -0.5760 -0.4320 \
		        2.1600 -2.0160 0.5760 -5.9040 3.3120 1.2960 -7.2000 -1.0080 \
		        1.2960 2.8800 -0.2880 7.6320 -4.0320 3.0240}
		FOREACH v ( BV V eps hole name ) { SET $v = $v IF(hole > 0) }
draw_fresnel   	# Draw a fresnel diffraction pattern
		image ( 21 , 21 ) -1 1 -1 1
		set y=0,20
		do x=0,20{
		   set r=sqrt((y- 10)**2 + ($x - 10)**2)
		   set image[$x,y] = J0(r)
		}
